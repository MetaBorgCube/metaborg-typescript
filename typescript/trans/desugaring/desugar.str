module desugar

imports
	signatures/-
	pp

signature
	constructors
		InterfaceDecl: String * TypeParameters * ObjectType -> InterfaceDecl
		ObjectType: List(TypeMember) -> ObjectType
		ObjectDecl: List(Property) -> ObjectDecl
		NoParameters: NoParameters
		NoReturnType: NoReturnType
		FunctionName: String -> FunctionName
		NoName: NoName
		Parameters: ParameterList -> Parameters
		ReturnType: TypeAnnotation -> ReturnType
		PAIR	: Type * Type -> PAIR
		ClassProperty: String * TypeAnnotation * Initializer -> ClassProperty
		UnionType: List(Type) -> UnionType

rules

	desugar: Interface(name, extends, fields, _) -> InterfaceDecl(name, extends, fields)
	desugar: Object(None()) -> ObjectType([])
	desugar: Object(Some(property)) -> ObjectType(property)
		
	desugar: TypeBodySemiColon(properties, _) -> properties
	desugar: TypeBodyComma(properties, _) -> properties
	desugar: TypeMemberListSemiColon(properties, _, property) -> [property | properties]
	desugar: TypeMemberListComma(properties, _, property) -> [property | properties]
	desugar: TypeMemberListSingle(property) -> [property]
	
	desugar: Union(UnionType(xs), z) -> UnionType(<concat> [xs , [z]]) 
	desugar: Union(type1, type2) -> UnionType([type1, type2])
	
	desugar: ObjectEmpty() -> ObjectDecl([])
	desugar: ObjectFields(fields) -> ObjectDecl(fields)
	desugar: ObjectFieldsTrailingComma(fields) -> ObjectDecl(fields)
	
	desugar: Initializer(type) -> type
	
	desugar: FunctionDeclaration(function, _) -> function
	
	desugar: Function(Some(name), callSignature, body) -> Function(FunctionName(name), callSignature, body)
	desugar: Function(None(), callSignature, body) -> Function(NoName(), callSignature, body)
	
  desugar: CallSignature(typeParameters, None(), None()) -> CallSignature(typeParameters, NoParameters(), NoReturnType())
  desugar: CallSignature(typeParameters, Some(parameters), None()) -> CallSignature(typeParameters, Parameters(parameters), NoReturnType())
  desugar: CallSignature(typeParameters, None(), Some(returnType)) -> CallSignature(typeParameters, NoParameters(), ReturnType(returnType))
  desugar: CallSignature(typeParameters, Some(parameters), Some(returnType)) -> CallSignature(typeParameters, Parameters(parameters), ReturnType(returnType))
  
  desugar: Module(Module(name, Some(body))) -> Module(name, body)
  desugar: Module(Module(name, None())) -> Module(name, [])
  
  desugar: ModuleItemsWithExport(items, item, items3) -> <concat> [items, [item], items3]
  desugar: ModuleItemsWithImport(items, item, items3) -> <concat> [items, [item], items3]
  
  desugar: ClassProperty(name, type, init, _) -> ClassProperty(name, type, init)
  desugar: MethodFunction(name, callSignature, body) -> ClassProperty(name, None(), Some(FunctionExpression(Function(NoName(), callSignature, body))))
  
  desugar: ClassDeclaration(name, tParams, Some(ClassExtends(extends)), members) -> ClassDeclaration(name, tParams, Some(extends), members)
strategies
  	desugar-all = bottomup(try(desugar))
  	
  	
  	
  	
  	
  	
  	
  	